apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'

def publicKeyPropertiesFile = rootProject.file("cortez/public_deployment/cortez_key.properties")
def zeronetPropertiesFile = rootProject.file("cortez/deployment/zeronet.properties")
def mainnetPropertiesFile = rootProject.file("cortez/deployment/mainnet.properties")
def alphanetPropertiesFile = rootProject.file("cortez/deployment/alphanet.properties")

def publicKeystoreMainnetProperties = new Properties()
def keystoreMainnetProperties = new Properties()
def keystoreZeronetProperties = new Properties()
def keystoreAlphanetProperties = new Properties()

publicKeystoreMainnetProperties.load(new FileInputStream(publicKeyPropertiesFile))
keystoreMainnetProperties.load(new FileInputStream(mainnetPropertiesFile))
keystoreZeronetProperties.load(new FileInputStream(zeronetPropertiesFile))
keystoreAlphanetProperties.load(new FileInputStream(alphanetPropertiesFile))

android {
    signingConfigs {
        publicConfig {
            keyAlias publicKeystoreMainnetProperties['keyAlias']
            keyPassword publicKeystoreMainnetProperties['keyPassword']
            storeFile file(publicKeystoreMainnetProperties['storeFile'])
            storePassword publicKeystoreMainnetProperties['storePassword']
        }
        mainnetConfig {
            keyAlias keystoreMainnetProperties['keyAlias']
            keyPassword keystoreMainnetProperties['keyPassword']
            storeFile file(keystoreMainnetProperties['storeFile'])
            storePassword keystoreMainnetProperties['storePassword']
        }
        zeronetConfig {
            keyAlias keystoreZeronetProperties['keyAlias']
            keyPassword keystoreZeronetProperties['keyPassword']
            storeFile file(keystoreZeronetProperties['storeFile'])
            storePassword keystoreZeronetProperties['storePassword']
        }
        alphanetConfig {
            keyAlias keystoreAlphanetProperties['keyAlias']
            keyPassword keystoreAlphanetProperties['keyPassword']
            storeFile file(keystoreAlphanetProperties['storeFile'])
            storePassword keystoreAlphanetProperties['storePassword']
        }
    }
    compileSdkVersion 28
    defaultConfig {
        applicationId 'com.tezcore.cortez'
        minSdkVersion 23
        targetSdkVersion 28
        versionCode 19
        versionName "1.4.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        targetCompatibility 1.8
        sourceCompatibility 1.8
    }
    flavorDimensions "version"
    productFlavors {
        publicMainnet {
            dimension "version"
            signingConfig signingConfigs.publicConfig
        }
        zeronet {
            applicationIdSuffix ".zeronet"
            versionNameSuffix "-zeronet"
            dimension "version"
            signingConfig signingConfigs.zeronetConfig
        }
        alphanet {
            applicationIdSuffix ".alphanet"
            versionNameSuffix "-alphanet"
            dimension "version"
            signingConfig signingConfigs.alphanetConfig
        }
        mainnet {
            dimension "version"
            signingConfig signingConfigs.mainnetConfig
        }
    }
}

ext {
    supportLibVersion = "28.0.0-alpha3"
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation "com.android.support:appcompat-v7:${supportLibVersion}"
    implementation 'com.android.support.constraint:constraint-layout:1.1.2'
    implementation "com.android.support:support-v4:${supportLibVersion}"
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
    implementation "com.android.support:design:${supportLibVersion}"
    implementation project(':ui')
}
